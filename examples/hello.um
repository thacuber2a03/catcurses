import (
	cc = "../catcurses.um"
)

const (
	exitKey = cc::Key('q')
	msg = "Hello from catcurses!"
)

x, y := 2, 3

fn main() {
	term := cc::stdTerminal()
	term.raw(true)
	term.echo(false)
	term.timeout(100)
	term.cursorVisibility(.hidden)

	term.enableColors()
	term.setColorHex(cc::darkGray, 0x444444)
	infoTextPair := term.addColorPair(.black, cc::darkGray)

	win := term.window
	win.keypad(true)

	for true {
		c, ok := win.getKey()
		if c == exitKey { break }

		switch c {
		case .left: x--
		case .right: x++
		case .up: y--
		case .down: y++
		}

		w, h := win.getSize()
		if y < 0 { y = 0 } else if y >= h { y = h-1 }
		if x < 0 { x = 0 } else if x >= w-len(msg) { x = w-len(msg) }

		win.clear()

		win.withAttrsAndColorPair({.italic}, infoTextPair, {
			win.printAt(0, 0, "Use the arrow keys to move the reverse text around\n")
			win.printAt(0, 1, "Press {} to exit\n", char(exitKey))
		})

		win.withAttrs({.reverse}, { win.printAt(x, y, msg) })

		win.refresh()
	}

	term.destroy()
}

